<!DOCTYPE html>
<html lang="en">
  <head>
    <title>GLFX Webcam Expts</title>
    <link rel="stylesheet" href="css/global-reset.css" />
    <link rel="stylesheet" href="css/app.css" />
  </head>

  <body>

    <div id="guideCanvasHolder">
      Key color<input type="color" id="keyColor" value="#00FF00" />
      <canvas id="guideCanvas"></canvas>
    </div>
    <main id="app" class="fullscreen">

        <canvas id="artCanvas" style="background-image: url(/img/AlbumPainting_01.jpg); background-size: cover;  max-width: initial"></canvas>
        
      </main>
      

    <!-- WEB CAM (hidden) -->
    <video autoplay="true" id="videoElement"></video>

    <script id="fragment-shader" type="glsl">
      precision mediump float;
      
      uniform sampler2D tex;
      uniform float texWidth;
      uniform float texHeight;
      
      uniform vec3 keyColor;
      uniform float similarity;
      uniform float smoothness;
      uniform float spill;
      
      // From https://github.com/libretro/glsl-shaders/blob/master/nnedi3/shaders/rgb-to-yuv.glsl
      vec2 RGBtoUV(vec3 rgb) {
        return vec2(
        rgb.r * -0.169 + rgb.g * -0.331 + rgb.b *  0.5    + 0.5,
        rgb.r *  0.5   + rgb.g * -0.419 + rgb.b * -0.081  + 0.5
        );
      }
      
      vec4 ProcessChromaKey(vec2 texCoord) {
        vec4 rgba = texture2D(tex, texCoord);
        float chromaDist = distance(RGBtoUV(texture2D(tex, texCoord).rgb), RGBtoUV(keyColor));
      
        float baseMask = chromaDist - similarity;
        float fullMask = pow(clamp(baseMask / smoothness, 0., 1.), 1.5);
        rgba.a = fullMask;
      
        float spillVal = pow(clamp(baseMask / spill, 0., 1.), 1.5);
        float desat = clamp(rgba.r * 0.2126 + rgba.g * 0.7152 + rgba.b * 0.0722, 0., 1.);
        rgba.rgb = mix(vec3(desat, desat, desat), rgba.rgb, spillVal);
      
        return rgba;
      }
  
      vec4 Posterize(in vec4 inputColor){
        float gamma = 0.99;
        float numColors = 5.0;
        
        vec3 c = inputColor.rgb;
        c = pow(c, vec3(gamma, gamma, gamma));
        c = c * numColors;
        c = floor(c);
        c = c / numColors;
        c = pow(c, vec3(1.0/gamma));
        
        return vec4(c, inputColor.a);
        }
      
      void main(void) {
        vec2 texCoord = vec2(gl_FragCoord.x/texWidth, 1.0 - (gl_FragCoord.y/texHeight));
  
        vec4 greenScreenColor = ProcessChromaKey(texCoord);
  
        gl_FragColor = Posterize(greenScreenColor);
      }
    </script>

    <script type="text/javascript" src="./libs/dat.gui.js"></script>
    <script type="text/javascript" src="./libs/stats.min.js"></script>
    <script type="text/javascript" src="./libs/glfx-full.js"></script>
    <script type="module" src="./js/index.js"></script>
  </body>
</html>
